apply plugin: 'java'
apply plugin: 'clojure'
apply plugin: 'idea'
apply plugin: 'application'

mainClassName = "rosetta.boot.Main"
applicationName = "rosetta-jvm"
archivesBaseName = "rosetta-jvm"

sourceCompatibility = 1.6
targetCompatibility = 1.6

warnOnReflection = true
aotCompile = true

defaultTasks('check')

configurations {
  rspec
  serviceTar
  serviceZip
}

artifacts {
  serviceTar distTar
  serviceZip distZip
}

dependencies {
  compile 'org.eclipse.jetty:jetty-servlet:8.1.7.v20120910'

  compile module('org.jruby.rack:jruby-rack:1.1.7') {
    dependency 'org.jruby:jruby-core:1.6.7.2'
    dependency 'org.jruby:jruby-stdlib:1.6.7.2'
    dependency 'com.jcraft:jzlib:1.1.1'
  }

  compile 'org.javafunk.funk:funk:0.1.16'

  compile 'org.rubygems:sinatra:1.3.3'
  compile 'org.rubygems:sinatra-contrib:1.3.2'

  compile 'org.clojure:clojure:1.4.0'

  compile 'org.codehaus.jackson:jackson-core-lgpl:1.9.0'
  compile 'org.codehaus.jackson:jackson-mapper-lgpl:1.9.0'

  compile "org.jruby:jruby-complete:1.6.7.2"
  compile "org.rubygems:gearup:0.0.5"
  compile 'org.rubygems:sprockets:2.4.5'
  compile 'org.rubygems:tilt:1.3.3'
  compile 'org.rubygems:coffee-script:2.2.0'
  compile 'org.rubygems:execjs:1.4.0'
  compile 'org.rubygems:therubyrhino:2.0.0'
  compile 'org.rubygems:less:2.2.1'
  compile 'org.rubygems:clamp:0.4.0'

  compile 'org.python:jython-standalone:2.5.3'

  compile 'org.ioke:ioke-lang:P-0.4.0-p11'
  compile 'org.ioke:ioke-lang-lib:P-0.4.0-p11'

  compile 'com.google.inject:guice:3.0'
  compile 'com.google.guava:guava:13.0.1'

  compile 'org.apache.commons:commons-lang3:3.1'
  compile 'org.apache.commons:commons-io:1.3.2'

  testCompile 'junit:junit:4.11'

  rspec 'org.jruby:jruby-complete:1.6.7.2'
  rspec 'org.rubygems:rspec:2.10.0'
  rspec 'org.rubygems:json:1.7.5'
}

sourceSets {
  main {
    clojure {
      srcDirs 'src/main/clojure'
    }
    resources {
      srcDirs 'src/main/ioke',
          'src/main/python',
          'src/main/ruby',
          'src/main/resources'
    }
  }
  test {
    resources {
      srcDirs 'src/test/ruby'
    }
  }
}

task(assets, type: JavaExec) {
  classpath runtimeClasspath
  main = "org.jruby.Main"
  args = ["classpath:bin/gearup", "src/main/resources"]
}

task(service, type: JavaExec) {
  classpath runtimeClasspath
  environment([
      'JYTHONPATH': sourceSets.main.resources.srcDirs.collect { it.toString() }.join(":")
  ])
  main = "org.python.util.jython"
  args = ["src/main/python/jigscraper/webservice.py", System.getProperty("username"), System.getProperty("rsa.token")]
}

task(jythonHelp, type: JavaExec) {
  classpath runtimeClasspath
  main = "org.python.util.jython"
  args = ["--help"]
}

task(iokeRun, type: JavaExec) {
  classpath runtimeClasspath
  main = "ioke.lang.Main"
  args = ["--", "src/main/ioke/helloworld.ik"]
}

task(iokeHelp, type: JavaExec) {
  classpath runtimeClasspath
  main = "ioke.lang.Main"
  args = ["--help"]
}

task(rspec, type: JavaExec) {
  main 'org.jruby.Main'
  classpath configurations.rspec + test.classpath
  args = ['classpath:bin/rspec', 'src/test/ruby']
}

check.dependsOn << 'rspec'
assemble.dependsOn << ['distTar', 'distZip']

idea.module {
  scopes.RUNTIME.plus += configurations.rspec
}
