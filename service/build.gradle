apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

mainClassName = "rosetta.Main"

sourceCompatibility = 1.6
targetCompatibility = 1.6

defaultTasks('check')

configurations {
  rspec
}

dependencies {
  compile "org.jruby:jruby-complete:1.6.7.2"
  compile "org.rubygems:gearup:0.0.5"
  compile 'org.rubygems:sprockets:2.4.5'
  compile 'org.rubygems:tilt:1.3.3'
  compile 'org.rubygems:coffee-script:2.2.0'
  compile 'org.rubygems:execjs:1.4.0'
  compile 'org.rubygems:therubyrhino:2.0.0'
  compile 'org.rubygems:less:2.2.1'
  compile 'org.rubygems:clamp:0.4.0'

  compile 'org.python:jython-standalone:2.5.3'

  compile 'org.ioke:ioke-lang:P-0.4.0-p11'
  compile 'org.ioke:ioke-lang-lib:P-0.4.0-p11'

  rspec 'org.jruby:jruby-complete:1.6.7.2'
  rspec 'org.rubygems:rspec:2.10.0'
}

sourceSets {
  main {
    resources {
      srcDirs 'src/main/ioke', 'src/main/python', 'src/main/ruby', 'src/main/resources'
    }
  }
  test {
    resources {
      srcDirs 'src/test/ruby'
    }
  }
}

task(assets, type: JavaExec) {
  classpath runtimeClasspath
  main = "org.jruby.Main"
  args = ["classpath:bin/gearup", "src/main/resources"]
}

task(service, type: JavaExec) {
  classpath runtimeClasspath
  environment([
      'JYTHONPATH': sourceSets.main.resources.srcDirs.collect { it.toString() }.join(":")
  ])
  main = "org.python.util.jython"
  args = ["src/main/python/jigscraper/webservice.py", System.getProperty("username"), System.getProperty("rsa.token")]
}

task(jythonHelp, type: JavaExec) {
  classpath runtimeClasspath
  main = "org.python.util.jython"
  args = ["--help"]
}

task(iokeRun, type: JavaExec) {
  classpath runtimeClasspath
  main = "ioke.lang.Main"
  args = ["--", "src/main/ioke/helloworld.ik"]
}

task(iokeHelp, type: JavaExec) {
  classpath runtimeClasspath
  main = "ioke.lang.Main"
  args = ["--help"]
}

task(rspec, type: JavaExec) {
  main 'org.jruby.Main'
  classpath configurations.rspec + test.classpath
  args = ['classpath:bin/rspec', 'src/test/ruby']
}

check.dependsOn << 'rspec'

idea.module {
  scopes.RUNTIME.plus += configurations.rspec
}
